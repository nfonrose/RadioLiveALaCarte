plugins {
    id 'java'
    id 'application'
}

group = 'com.prtlabs.rlalc.backend.services.mediacapture'
version = '0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation project(":rlalc-backend-common")

    // Google Guice for dependency injection
    implementation 'com.google.inject:guice:7.0.0'

    // Jakarta Inject API
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'

    // Logs (SLF4J, LogBack, SpringBoot like colors)
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.20'
    implementation 'org.fusesource.jansi:jansi:2.4.1'                             // For cross-platform ANSI color support

    // Jackson for JSON processing
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'

    // Quartz for job scheduling
    implementation 'org.quartz-scheduler:quartz:2.3.2'

    // Lombok for reducing boilerplate code
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

application {
    mainClass = 'com.prtlabs.rlalc.backend.mediacapture.entrypoint.EntryPoint'

    // Build the "Java System properties" list dynamically
    applicationDefaultJvmArgs = [
        '-Djansi.passthrough=true'                             // To let ANSI colors passthrough when using `gradle run`
    ]
    if (project.hasProperty('prt.rlalc.baseDir')) {               applicationDefaultJvmArgs << "-Dprt.rlalc.baseDir=${project.property('prt.rlalc.baseDir')}" }
    if (project.hasProperty('prt.rlalc.confFileAbsolutePath')) {  applicationDefaultJvmArgs << "-Dprt.rlalc.confFileAbsolutePath=${project.property('prt.rlalc.confFileAbsolutePath')}" }
}

// Create a fatJar (without shadow, so not optimized) which can be used with 'java -jar ./build/libs/rlalc-backend-media-capture-service-1.0-SNAPSHOT.jar'
jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '8.10'
}

// Docker tasks
task prtBuildDockerImage(type: Exec) {
    dependsOn jar
    group = 'docker'
    description = 'Builds the Docker image for the application'

    commandLine 'docker', 'build', '-t', "prt-rlalc-backend-media-capture-service:${version}", '.'

    doFirst {
        println "Building Docker image [prt-rlalc-backend-media-capture-service:${version}]"
        println " -> Equivalent CLI command:"
        println "      docker build -t prt-rlalc-backend-media-capture-service:${version} .\n"
    }
}

task prtRunViaDocker(type: Exec) {
    group = 'docker'
    description = 'Runs the application using Docker'

    commandLine 'docker', 'run', '--rm', '-p', '8080:8080', 
                '-v', "${projectDir}/logs:/opt/prtlabs/radioLiveALaCarte/logs", 
                '-v', "${projectDir}/media:/opt/prtlabs/radioLiveALaCarte/media",
                "prt-rlalc-backend-media-capture-service:${version}"

    doFirst {
        println "Running Docker container [prt-rlalc-backend-media-capture-service:${version}]"
        println " -> Equivalent CLI command:"
        println "      docker run --rm -p 8080:8080 -v ${projectDir}/logs:/opt/prtlabs/radioLiveALaCarte/logs -v ${projectDir}/media:/opt/prtlabs/radioLiveALaCarte/media prt-rlalc-backend-media-capture-service:${version}\n"
    }
}


task prtHelp {
    group = 'Help'
    description = 'Shows how to run the application with project (-P) properties for RadioLiveALaCarte.'

    doLast {
        println '''
    ────────────────────────────────────────────────────────────
     RadioLiveALaCarte - PRT Helper
    ────────────────────────────────────────────────────────────

    Usage:
      ./gradlew run -Pprt.rlalc.baseDir=/path/to/@opt-prtlabs \\
                    -Pprt.rlalc.confFileAbsolutePath=/path/to/conf/rlalc-media-capture.conf

    Description of properties:
      -Pprt.rlalc.baseDir                → Base directory of your local installation.
      -Pprt.rlalc.confFileAbsolutePath   → Absolute path to the configuration file.

    Example:
      ./gradlew run -Pprt.rlalc.baseDir=/Users/teevity/Dev/misc/@opt-prtlabs \\
                    -Pprt.rlalc.confFileAbsolutePath=/Users/teevity/Dev/misc/@opt-prtlabs/radiolivealacarte/conf/rlalc-media-capture-batch-example001.conf

    You can also run without any -P options, in which case defaults will apply.

    ────────────────────────────────────────────────────────────
        '''
    }
}
